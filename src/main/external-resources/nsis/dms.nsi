; Java Launcher with automatic JRE installation
; Use javaw.exe to avoid DOS box or java.exe to keep stdout/stderr
;------------------------------------------------------------------

Unicode "true"
SetCompressor lzma
SetCompressorDictSize 64
RequestExecutionLevel user
SilentInstall silent
AutoCloseWindow true
ShowInstDetails nevershow

; Include the project header file generated by the nsis-maven-plugin

!include "..\..\..\..\target\project.nsh"
!include "${PROJECT_BUILD_DIR}\extra.nsh"
!include "FileFunc.nsh"
!include "LogicLib.nsh"
!include "SearchJava.nsh"
!include "WinVer.nsh"
!include "WordFunc.nsh"
!include "x64.nsh"

Name "${PROJECT_NAME_SHORT}"
Caption "${PROJECT_NAME}"
Icon "${PROJECT_BASEDIR}\src\main\resources\images\logo.ico"

!searchparse /noerrors '${PROJECT_VERSION}' '' PRODUCT_VERSION_MAJOR '.' PRODUCT_VERSION_MINOR '.' PRODUCT_VERSION_BUGFIX '-'
!define PRODUCT_VERSION_SHORT "${PRODUCT_VERSION_MAJOR}.${PRODUCT_VERSION_MINOR}.${PRODUCT_VERSION_BUGFIX}.0"
VIAddVersionKey "ProductName" "${PROJECT_NAME}"
VIAddVersionKey "Comments" "Media server"
VIAddVersionKey "CompanyName" "${PROJECT_ORGANIZATION_NAME}"
VIAddVersionKey "LegalTrademarks" ""
VIAddVersionKey "LegalCopyright" ""
VIAddVersionKey "FileDescription" "${PROJECT_NAME}"
VIAddVersionKey "FileVersion" "${PROJECT_VERSION}"
VIAddVersionKey "ProductVersion" "${PROJECT_VERSION}"
;https://msdn.microsoft.com/en-us/library/windows/desktop/ms646997%28v=vs.85%29.aspx
VIProductVersion "${PRODUCT_VERSION_SHORT}"

Section
	${SearchJava}

	ReadRegStr $R3 HKCU "SOFTWARE\${PROJECT_NAME}" "HeapMem"
	StrCmp $R3 "" 0 +2
	StrCpy $R3 "768M"
	StrCpy $R3 "-Xmx$R3"

	; Get the command line parameters
	${GetParameters} $1

	SetOutPath $EXEDIR
	Exec '"$JavaLocation" -classpath update.jar;${PROJECT_ARTIFACT_ID}.jar $R3 -Djava.net.preferIPv4Stack=true -Dfile.encoding=UTF-8 net.pms.PMS $1'
SectionEnd

Function .onInit
	${If} ${RunningX64}
		SetRegView 64
	${EndIf}

	${IfNot} ${AtLeastWinXP}
		MessageBox MB_ICONSTOP "Windows XP and above is required"
		Quit
	${EndIf}
FunctionEnd
